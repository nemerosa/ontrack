buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
    }
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'

dependencies {
    testCompile project(':ontrack-client')
    testCompile project(':ontrack-dsl')
    testCompile project(':ontrack-dsl-shell')
    testCompile project(':ontrack-test-utils')
    testCompile 'org.codehaus.groovy:groovy-all'
    testCompile 'org.springframework.boot:spring-boot-starter'

    testCompile 'org.seleniumhq.selenium:selenium-java:2.53.0'
}

/**
 * Packaging
 */

task testJar(type: Jar) {
    from sourceSets.test.output
}

artifacts {
    archives testJar
}

bootRepackage {
    mainClass = 'net.nemerosa.ontrack.acceptance.boot.Start'
    withJarTask = 'testJar'
    customConfiguration = 'testRuntime'
}

task normaliseJar {
    doFirst {
        // Used for local testing only
        ant.copy file: testJar.outputs.files.singleFile, tofile: "${buildDir}/libs/ontrack-acceptance.jar"
    }
}
normaliseJar.dependsOn bootRepackage
assemble.dependsOn normaliseJar

/**
 * Local test definitions
 */

ext {
    ontrackUrl = 'http://localhost:8080'
    ontrackJvmOptions = project.hasProperty('ontrackJvmOptions') ? project.ontrackJvmOptions : '-Xmx256m'
}

acceptanceTest {
    outputs.upToDateWhen { false }  // Always run tests
    systemProperty 'ontrack.url', ontrackUrl
    systemProperty 'ontrack.implicitWait', project.hasProperty('ontrackImplicitWait') ? ontrackImplicitWait : 5
}

// Disable unit tests (none in this project)
test {
    enabled = false
}

// Disable normal JAR creation
jar {
    enabled = false
}
