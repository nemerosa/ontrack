buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
    }
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'

ext {
    seleniumVersion = '3.3.1'
}

dependencies {
    testCompile project(':ontrack-client')
    testCompile project(':ontrack-dsl')
    testCompile project(':ontrack-dsl-shell')
    testCompile project(':ontrack-test-utils')
    testCompile 'org.codehaus.groovy:groovy-all'
    testCompile 'org.springframework.boot:spring-boot-starter'

    testCompile "org.seleniumhq.selenium:selenium-java:${seleniumVersion}"
    testCompile "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.seleniumhq.selenium' && details.requested.name != 'htmlunit-driver') {
            details.useVersion seleniumVersion
        }
    }
}

/**
 * Packaging
 */

task testJar(type: Jar) {
    from sourceSets.test.output
}

artifacts {
    archives testJar
}

bootRepackage {
    mainClass = 'net.nemerosa.ontrack.acceptance.boot.Start'
    withJarTask = 'testJar'
    customConfiguration = 'testRuntime'
    classifier = 'app'
}

task normaliseJar {
    dependsOn bootRepackage
    doFirst {
        project.mkdir "${buildDir}/libs"
        // Used for local testing only
        ant.copy file: bootRepackage.outputs.files.singleFile, tofile: "${buildDir}/libs/ontrack-acceptance.jar"
    }
}

task acceptanceDockerPrepareEnv(type: Copy) {
    dependsOn normaliseJar
    from "${buildDir}/libs/ontrack-acceptance.jar"
    into "${projectDir}/src/main/docker"
}

assemble.dependsOn normaliseJar

task dockerBuild(type: Exec, dependsOn: acceptanceDockerPrepareEnv) {
    executable 'docker'
    args = [
            'build',
            '--tag',
            "nemerosa/ontrack-acceptance:${versioning.info.display}",
            project.file('src/main/docker')
    ]
}

task dockerLatest(type: Exec, dependsOn: dockerBuild) {
    executable 'docker'
    args = [
            'tag',
            "nemerosa/ontrack-acceptance:${versioning.info.display}",
            "nemerosa/ontrack-acceptance:latest",
    ]
}

rootProject.tasks.publicationPackage {
    from bootRepackage
}

/**
 * Local test definitions
 */

ext {
    ontrackUrl = 'http://localhost:8080'
    ontrackJvmOptions = project.hasProperty('ontrackJvmOptions') ? project.ontrackJvmOptions : '-Xmx256m'
}

acceptanceTest {
    outputs.upToDateWhen { false }  // Always run tests
    systemProperty 'ontrack.url', ontrackUrl
    systemProperty 'ontrack.implicitWait', project.hasProperty('ontrackImplicitWait') ? ontrackImplicitWait : 5
}

// Disable unit tests (none in this project)
test {
    enabled = false
}

// Disable normal JAR creation

jar {
    enabled = false
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            // Clears all previous artifacts defined in root `build.gradle`
            artifacts = [testJar]
        }
    }
}
