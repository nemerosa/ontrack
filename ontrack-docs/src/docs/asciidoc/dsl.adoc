[[dsl]]
== DSL

Ontrack provides several ways of interaction:

* the graphical user interface (GUI)
* the REST API (UI - also used internally by the GUI)
* the Domain Specific Language (DSL)

Using the DSL, you can write script files which interact remotely with your
Ontrack instance.

[[dsl-usage]]
=== DSL Usage

In some cases, like when using the
https://github.com/nemerosa/ontrack-jenkins[Ontrack Jenkins plug-in], you can
just write some Ontrack DSL to use it, because the configuration would have
been done for you.

In some other cases, you have to set-up the Ontrack DSL environment yourself.

[[dsl-usage-embedded]]
==== Embedded

You can embed the Ontrack DSL in your own code by importing it.

Using Maven:

      <dependencies>
         <groupId>net.nemerosa.ontrack</groupId>
         <artifactId>ontrack-dsl</artifactId>
         <version>{{ontrack-version}}</version>
      </dependencies>

Using Gradle:

      compile 'net.nemerosa.ontrack:ontrack-dsl:{{ontrack-version}}'

[[dsl-usage-shell]]
==== Standalone shell

Future versions of https://github.com/nemerosa/ontrack/issues/198[Ontrack] will
have a standalone executable JAR that you can call from the shell.

[[dsl-usage-connection]]
==== Connection

Before calling any DSL script, you have to configure an `Ontrack` instance
which will connect to your remote Ontrack location:

[source,java]
----
import net.nemerosa.ontrack.dsl.*;

String url = "http://localhost:8080";
String user = "admin";
String password = "admin";

Ontrack ontrack = OntrackConnection.create(url)
   // Logging
   .logger(new OTHttpClientLogger() {
      public void trace(String message) {
         System.out.println(message);
      }
   })
   // Authentication
   .authenticate(user, password)
   // OK
   .build();
----

[[dsl-usage-calling]]
==== Calling the DSL

The Ontrack DSL is expressed through Groovy and can be called using the
`GroovyShell`:

[source,java]
----
import groovy.lang.Binding;
import groovy.lang.GroovyShell;

Ontrack ontrack = ...

Map<String, Object> values = new HashMap<>();
values.put("ontrack", ontrack);
Binding binding = new Binding(values);

GroovyShell shell = new GroovyShell(binding);

Object shellResult = shell.evaluate(script);
----

[[dsl-usecases-security]]
=== DSL Security

The DSL allows to manage the <<security,accounts>> and the <<security,account groups>>.

[[dsl-usecases-security-accounts]]
==== Management of accounts

To add or update a _built-in_ account:

[source,groovy]
----
ontrack.admin.account(
   "dcoraboeuf",              // Name
   "Damien Coraboeuf",        // Display name
   "dcoraboeuf@nemerosa.net", // Email
   "my-secret-password",      // Password
   [                          // List of groups (optional)
      "Group1",
      "Group2"
   ]
)
----

To get the list of accounts:

[source,groovy]
----
def accounts = ontrack.admin.accounts
def account = accounts.find { it.name == 'dcoraboeuf' }
assert account != null
assert account.fullName == "Damien Coraboeuf"
assert account.email == "dcoraboeuf@nemerosa.net"
assert account.authenticationSource.allowingPasswordChange
assert account.authenticationSource.id == "password"
assert account.authenticationSource.name == "Built-in"
assert account.role == "USER"
assert account.accountGroups.length == 2
----

IMPORTANT: LDAP accounts cannot be created directly. See the <<security,documentation>> for more details.

[[dsl-usecases-security-account-permissions]]
==== Account permissions

To give a role to an account:

[source,groovy]
----
ontrack.admin.setAccountGlobalPermission(
   'dcoraboeuf', "ADMINISTRATOR
)
ontrack.project('PROJECT')
ontrack.admin.setAccountProjectPermission(
   'PROJECT', 'dcoraboeuf', "OWNER
)
----

To get the list of permissions for an account:

[source,groovy]
----
def permissions = ontrack.admin.getAccountProjectPermissions('PROJECT', 'dcoraboeuf')
assert permissions != null
assert permissions.size() == 1
assert permissions[0].id == 'OWNER'
assert permissions[0].name == 'Project owner'
----

[[dsl-usecases-security-account-groups]]
==== Management of account groups

To add or update an account group:

[source,groovy]
----
ontrack.admin.accountGroup('Administrators', "Group of administrators")
----

To get the list of groups:

[source,groovy]
----
def groups = ontrack.admin.groups
def group = groups.find { it.name == 'Administrators' }
assert group.name == 'Administrators'
assert group.description == "Group of administrators"
----

[[dsl-usecases-security-account-group-permissions]]
==== Account group permissions

To give a role to an account group:

[source,groovy]
----
ontrack.admin.setAccountGroupGlobalPermission(
   'Administrators', "ADMINISTRATOR"
)
ontrack.project('PROJECT')
ontrack.admin.setAccountGroupProjectPermission(
   'PROJECT', 'Administrators', "OWNER"
)
----

To get the list of permissions for an account group:

[source,groovy]
----
def permissions = ontrack.admin.getAccountGroupProjectPermissions('PROJECT', 'Administrators')
assert permissions != null
assert permissions.size() == 1
assert permissions[0].id == 'OWNER'
assert permissions[0].name == 'Project owner'
----

[[dsl-usecases-ldap-mapping]]
=== DSL LDAP mapping

The <<ldap,LDAP mappings>> can be generated using the DSL.

To add or update a LDAP mapping:

[source,groovy]
----
ontrack.admin.ldapMapping 'ldapGroupName', 'groupName'
----

To get the list of LDAP mappings:

[source,groovy]
----
LDAPMapping mapping = ontrack.admin.ldapMappings[0]
assert mapping.name == 'ldapGroupName'
assert mapping.groupName == 'groupName'
----

[[dsl-usecases-images]]
=== DSL Images and documents

Some resources can be associated with images (like promotion levels and validation stamps) and some documents can be
downloaded.

When uploading a document or an image, the DSL will accept any object (see below), optionally associated with a MIME
content type (the content type is either read from the source object or defaults to `image/png`).

The object can be any of:

* a `URL` object - the MIME type and the binary content will be downloaded using the URL - the URL must be accessible
  anonymously
* a `File` object - the binary content is read from the file and the MIME type must be provided
* a valid URL string - same as an `URL` - see above
* a file path - same as a `File` - see above

For example:

[source,groovy]
----
ontrack.project('project') {
   branch('branch') {
      promotionLevel('COPPER', 'Copper promotion') {
         image '/path/to/local/file.png', 'image/png'
      }
   }
}
----

Document and image downloads return a `Document` object with has two properties:

* `content` - byte array
* `type` - MIME content type

For example, to store a promotion level's image into a file:

[source,groovy]
----
File file = ...
def promotionLevel = ontrack.promotionLevel('project', 'branch', 'COPPER')
file.bytes = promotionLevel.image.content
----

[[dsl-usecases-changelogs]]
=== DSL Change logs

When a branch is configured for a SCM (Git, Subversion), a
<<changelogs,change log>> can be computed between two builds and following
collections can be displayed:

* revisions or commits
* issues
* file changes

TIP: Change logs can also be computed between builds which belong to different
     branches, as long as they are in the same project. _This is only
     supported for Git, not for Subversion._

[[dsl-usecases-changelogs-get]]
==== Getting the change log

Given two builds, one gets access to the change log using:

[source,groovy]
----
def build1 = ontrack.build('proj', 'master', '1')
def build2 = ontrack.build('proj', 'master', '2')

def changelog = build1.getChangeLog(build2)
----

NOTE: The returned change log might be `null` if the project and branches are
      not correctly configured.

On the returned `ChangeLog` object, one can access commits, issues and file
changes.

[[dsl-usecases-changelogs-commits]]
==== Commits

The list of commits can be accessed using the `commits` property:

[source,groovy]
----
changeLog.commits.each {
    println "* ${it.shortId} ${it.message} (${it.author} at ${it.timestamp})"
}
----

Each item in the `commits` collection has the following properties:

* `id` - identifier, revision or commit hash
* `shortId` - short identifier, revision or abbreviated commit hash
* `author` - name of the committer
* `timestamp` - ISO date for the commit time
* `message` - raw message for the commit
* `formattedMessage` - HTML message with links to the issues
* `link` - link to the commit

NOTE: This covers only the common attributes provided by Ontrack - additional
      properties are also available for a specific SCM.

[[dsl-usecases-changelogs-issues]]
==== Issues

The list of issues can be accessed using the `issues` property:

[source,groovy]
----
changeLog.issues.each {
    println "* ${it.displayKey} ${it.status} ${it.summary}"
}
----

Each item in the `issues` collection has the following properties:

* `key` - identifier, like `1`
* `displayKey` - display key (like `#1`)
* `summary` - short title for the issue
* `status` - status of the issue
* `url` - link to the issue

NOTE: This covers only the common attributes provided by Ontrack - additional
      properties are also available for a specific issue service.

[[dsl-usecases-changelogs-export]]
==== Exporting the change log

The change log can also be exported as text (HTML and Markdown are also
available):

[source,groovy]
----
String text = changeLog.exportIssues(
        format: 'text',
        groups: [
                'Bugs'        : ['defect'],
                'Features'    : ['feature'],
                'Enhancements': ['enhancement'],
        ],
        exclude: ['design', 'delivery']
)
----

* `format` can be one of `text` (default), `html` or `markdown`
* `groups` allows to group issues per type. If not defined, no grouping is done
* `exclude` defines the types of issues to not include in the change log
* `altGroup` defaults to _Other_ and is the name of the group where remaining
   issues do not fit.

[[dsl-usecases-changelogs-files]]
==== File changes

The list of file changes can be accessed using the `files` property:

[source,groovy]
----
changeLog.files.each {
    println "* ${it.path} (${it.changeType})"
}
----

Each item in the `files` collection has the following properties:

* `path` - path changed
* `changeType` - nature of the change
* `changeTypes` - list of changes on this path

NOTE: This covers only the common attributes provided by Ontrack - additional
      properties are also available for a specific SCM.

[[dsl-templates]]
=== DSL Branch template definitions

Using the `template(Closure)` method on a branch, one can define the template
definition for a branch.

For example:

[source,groovy]
----
template {
   parameter 'gitBranch', 'Name of the Git branch', 'release/${sourceName}'
   fixedSource '1.0', '1.1'
}
----

* `def parameter(String name, String description = '', String expression = '')`
  — defines a parameter for the template, with an optional expression based on
  a source name

* `def fixedSource(String... names)` — sets a synchronization source on the
  template, based on a fixed list of names

You can then use this branch definition in order to generate or update branches
from it:

[source,groovy]
----
// Create a template
ontrack.branch('project', 'template') {
   template {
      parameter 'gitBranch', 'Name of the Git branch', 'release/${sourceName}'
   }
}
// Creates or updates the TEST instance
ontrack.branch('project', 'template').instance 'TEST', [
   gitBranch: 'my-branch'
]
----

[[dsl-scm]]
=== DSL SCM extensions

If a SCM (<<usage-subversion,Subversion>> or <<usage-git,Git>>) is correctly configured on a branch, it is possible
to download some files.

IMPORTANT: This is allowed only for the project owner.

For example, the following <<dsl-branch-download,call>>:

[source,groovy]
----
def text = ontrack.branch('project', 'branch').download('folder/subfolder/path.txt')
----

will download the `folder/subfolder/path.txt` file from the corresponding SCM branch. A `OTNotFoundException`
exception is thrown if the file cannot be found.

[[dsl-reference]]
=== DSL Reference

See the <<dsl-doc,appendixes>>.

[[dsl-samples]]
=== DSL Samples

Creating a build:

[source,groovy]
----
ontrack.branch('project', 'branch').build('1', 'Build 1')
----

Promoting a build:

[source,groovy]
----
ontrack.build('project', '1', '134').promote('COPPER')
----

Validating a build:

[source,groovy]
----
ontrack.build('project', '1', '134').validate('SMOKETEST', 'PASSED')
----

Getting the last promoted build:

[source,groovy]
----
def buildName = ontrack.branch('project', 'branch').lastPromotedBuilds[0].name
----

Getting the last build of a given promotion:

[source,groovy]
----
def branch = ontrack.branch('project', 'branch')
def builds = branch.standardFilter withPromotionLevel: 'BRONZE'
def buildName = builds[0].name
----

Configuring a whole branch:

[source,groovy]
----
ontrack.project('project') {
   branch('1.0') {
      promotionLevel 'COPPER', 'Copper promotion'
      promotionLevel 'BRONZE', 'Bronze promotion'
      validationStamp 'SMOKE', 'Smoke tests'
   }
}
----

Creating a branch template and an instance out of it:

[source,groovy]
----
// Branch template definition
ontrack.project(project) {
   config {
      gitHub 'ontrack'
   }
   branch('template') {
      promotionLevel 'COPPER', 'Copper promotion'
      promotionLevel 'BRONZE', 'Bronze promotion'
      validationStamp 'SMOKE', 'Smoke tests'
      // Git branch
      config {
         gitBranch '${gitBranch}'
      }
      // Template definition
      template {
         parameter 'gitBranch', 'Name of the Git branch'
      }
   }
}
// Creates a template instance
ontrack.branch(project, 'template').instance 'TEST', [
   gitBranch: 'feature/test'
]
----
