/**
 * Packaging of Ontrack for Linux OS (RPM + Debian)
 */

/**
 * The package version does not accept versions like the ones generated
 * from the Versioning plugin for the feature branches for example.
 */
def packageVersion
if (versioning.info.display ==~ /\d+\.\d+\.\d+/) {
    packageVersion = versioning.info.display
} else {
    packageVersion = '0.0.0'
}
println "Using package version = ${packageVersion}"

ospackage {
    packageName = 'ontrack'
    version = packageVersion
    release = '1'
    os = LINUX // only applied to RPM

    preInstall file('gradle/os-package/preInstall.sh')
    postInstall file('gradle/os-package/postInstall.sh')

    from(project(':ontrack-ui').files("build/libs/ontrack-ui-${versioning.info.display}.jar")) {
        into '/opt/ontrack/lib'
        rename '.*', 'ontrack.jar'
    }

    from('gradle/os-package') {
        include 'ontrack.sh'
        into '/opt/ontrack/bin'
        fileMode = 0550
    }

}

task debPackage(type: Deb, dependsOn: ':ontrack-ui:bootRepackage') {
    link '/etc/init.d/ontrack', '/opt/ontrack/bin/ontrack.sh'
}

task rpmPackage(type: Rpm, dependsOn: ':ontrack-ui:bootRepackage') {
    user = 'ontrack'
    link '/etc/init.d/ontrack', '/opt/ontrack/bin/ontrack.sh'
}

task osPackages {
    dependsOn rpmPackage
    dependsOn debPackage
}
