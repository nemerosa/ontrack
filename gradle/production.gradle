/**
 * Production tasks
 */

import net.nemerosa.ontrack.gradle.DockerStart
import net.nemerosa.ontrack.gradle.DockerStop
import net.nemerosa.ontrack.gradle.DOSetup
import net.nemerosa.ontrack.gradle.RemoteAcceptanceTest

task productionSetup(type: DOSetup) {
    apiToken = project.properties.digitalOceanAccessToken
    dropletName = project.properties.productionMachine
    size = '1gb'
}

task productionEnv {
    mustRunAfter productionSetup
    doFirst {
        project.exec {
            executable 'docker-machine'
            args = ['ssh', project.properties.productionMachine, 'mkdir -p /var/ontrack']
        }
        project.exec {
            executable 'docker-machine'
            args = ['scp', '-r', project.properties.productionConf, "${project.properties.productionMachine}:/var/ontrack/conf"]
        }
    }
}

task productionStop(type: DockerStop) {
    containerName = 'ontrack'
    remove = false
    removeVolumes = false
    doLast {
        docker 'rename', 'ontrack', "ontrack-${new Date()}"
    }
}

task productionStart(type: DockerStart) {
    dependsOn 'productionStop'
    machine = project.properties.productionMachine
    exposePort = true
    containerName = 'ontrack'
    image = "nemerosa/ontrack:${project.properties.ontrackVersion}"
    conf = file('/var/ontrack/conf')
    profile = 'prod'
    mustRunAfter productionEnv
}

task productionTest(type: RemoteAcceptanceTest) {
    disableSsl = false
    acceptanceContext = 'production'
    acceptanceUrl = project.properties.productionUrl
    acceptanceTimeout = 300
    acceptanceImplicitWait = 30
}
