/**
 * Production tasks
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.nemerosa:http-client-json:1.1.0'
        classpath 'net.nemerosa.ontrack:ontrack-dsl:2.12.4'
    }
}

import net.nemerosa.httpclient.*
import net.nemerosa.httpclient.json.*
import net.nemerosa.ontrack.gradle.DOSetup
import net.nemerosa.ontrack.gradle.DockerStart
import net.nemerosa.ontrack.gradle.DockerStop
import net.nemerosa.ontrack.gradle.RemoteAcceptanceTest

class ProductionVersion extends DefaultTask {

    private String version

    @TaskAction
    void run() {
        def client = new JsonClientImpl(
                ClientBuilder
                        .create(project.properties.productionUrl, false)
                        .withLogger({ println "[${name}][HTTP] ${it}" })
                        .build(),
                true
        )
        try {
            version = client.get('info').version.full.asText()
        } catch (ClientGeneralException ignored) {
            println "[${name}] Cannot connect to the production URL at ${project.properties.productionUrl}"
            version = null
        }
        println "[${name}] Version = ${version}"
    }

    String getVersion() {
        return version
    }
}

task productionSetup(type: DOSetup) {
    apiToken = project.properties.digitalOceanAccessToken
    dropletName = project.properties.productionMachine
    region = 'fra1'
    size = '1gb'
}

task productionEnv {
    mustRunAfter productionSetup
    doFirst {
        project.exec {
            executable 'docker-machine'
            args = ['ssh', project.properties.productionMachine, 'mkdir -p /var/ontrack']
        }
        project.exec {
            executable 'docker-machine'
            args = ['ssh', project.properties.productionMachine, 'mkdir -p /var/ontrack/data']
        }
        project.exec {
            executable 'docker-machine'
            args = ['scp', '-r', project.properties.productionConf, "${project.properties.productionMachine}:/var/ontrack/conf"]
        }
    }
}

task productionVersion(type: ProductionVersion)

task productionStop(type: DockerStop) {
    // FIXME Backup according to version number
    machine = project.properties.productionMachine
    containerName = 'ontrack'
    remove = true
    removeVolumes = false
    ignoreError = true
    logFile = null
}

task productionStart(type: DockerStart) {
    dependsOn 'productionStop'
    machine = project.properties.productionMachine
    exposePort = true
    containerName = 'ontrack'
    image = "nemerosa/ontrack:${project.properties.ontrackVersion}"
    conf = file('/var/ontrack/conf') // Dir on the host, provisioned initially by productionEnv
    // FIXME Data is cleared on startup...
    data = file('/var/ontrack/data') // Dir on the host, filled in by the application itself
    profile = 'prod'
    mustRunAfter productionEnv
}

task productionTest(type: RemoteAcceptanceTest) {
    // FIXME Production test must use a valid SSL certificate
    disableSsl = true
    acceptanceContext = 'production'
    acceptanceUrl = project.properties.productionUrl
    acceptanceTimeout = 300
    acceptanceImplicitWait = 30
}

task productionInstall {
    dependsOn productionSetup
    dependsOn productionEnv
    dependsOn productionStart
    dependsOn productionTest
}

task productionUpgrade {
    dependsOn productionStart
    dependsOn productionTest
}
