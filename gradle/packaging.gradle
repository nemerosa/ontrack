/**
 * Packaging of Ontrack for the subsequent stages of the validation.
 *
 * This creates a ZIP which contains:
 *
 * - all Gradle files needed for the execution of the pipeline
 * - the `buildSrc` which contains the Gradle helper classes
 * - the UI JAR artifact
 * - the Acceptance JAR artifact
 * - a ZIP containing ALL artifacts (POM files, JAR, Javadoc & Sources)
 * - an `ontrack.properties` file which contains the list of modules & the version information
 */

apply plugin: 'base'

// ZIP package which contains all artifacts to be published

task publicationPackage(type: Zip) {
    classifier = 'publication'
    archiveName = 'ontrack-publication.zip'
    subprojects {
        afterEvaluate {
            if (tasks.findByName('jar')) {
                dependsOn assemble
                from jar
                if (tasks.findByName('javadocJar')) from javadocJar
                if (tasks.findByName('sourceJar')) from sourceJar
                if (tasks.findByName('pomFile')) from pomFile.output
            }
        }
    }
}

// Ontrack descriptor

task deliveryDescriptor {
    ext.output = project.file('build/ontrack.properties')
    doLast {
        output.text = "# Ontrack properties\n"
        // Version
        output << "# Version information"
        output << "version.build = ${project.versioning.info.build}\n"
        output << "version.branch = ${project.versioning.info.branch}\n"
        output << "version.base = ${project.versioning.info.base}\n"
        output << "version.branchId = ${project.versioning.info.branchId}\n"
        output << "version.branchType = ${project.versioning.info.branchType}\n"
        output << "version.commit = ${project.versioning.info.commit}\n"
        output << "version.display = ${project.versioning.info.display}\n"
        output << "version.full = ${project.versioning.info.full}\n"
        output << "version.scm = ${project.versioning.info.scm}\n"
        // Modules
        output << "# Comma-separated list of modules\n"
        output << "modules = ${project.subprojects.findAll { it.tasks.findByName('jar') }.collect { it.name }.join(',')}\n"
    }
}

// Delivery package

task deliveryPackage(type: Zip) {
    classifier = 'delivery'
    // Gradle files
    from(projectDir) {
        include 'buildSrc/**'
        include '*.gradle'
        include 'gradlew*'
        include 'gradle/**'
        include 'gradle.properties'
        exclude '**/.gradle/**'
        exclude '**/build/**'
    }
    // All artifacts
    dependsOn publicationPackage
    from publicationPackage
    // Descriptor
    dependsOn deliveryDescriptor
    from deliveryDescriptor.output
}

build.dependsOn deliveryPackage