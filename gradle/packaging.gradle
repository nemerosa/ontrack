/**
 * Packaging of Ontrack for the subsequent stages of the validation.
 *
 * This creates a ZIP which contains:
 *
 * - all Gradle files needed for the execution of the pipeline
 * - the `buildSrc` which contains the Gradle helper classes
 * - the UI JAR artifact
 * - the Acceptance JAR artifact
 * - a ZIP containing ALL artifacts (POM files, JAR, Javadoc & Sources)
 * - an `ontrack.yml` file which contains the list of modules & the version information
 */

apply plugin: 'base'

// ZIP package which contains all artifacts to be published

task publicationPackage(type: Zip) {
    classifier = 'publication'
    archiveName = 'ontrack-publication.zip'
    subprojects {
        afterEvaluate {
            if (tasks.findByName('jar')) {
                dependsOn assemble
                from jar
                if (tasks.findByName('javadocJar')) from javadocJar
                if (tasks.findByName('sourceJar')) from sourceJar
                if (tasks.findByName('pomFile')) from pomFile.output
            }
        }
    }
}

// Delivery package

task deliveryPackage(type: Zip) {
    classifier = 'delivery'
    // Gradle files
    from(projectDir) {
        include 'buildSrc/**'
        include '*.gradle'
        include 'gradlew*'
        include 'gradle/**'
        include 'gradle.properties'
        exclude '**/.gradle/**'
        exclude '**/build/**'
    }
    // All artifacts
    dependsOn publicationPackage
    from publicationPackage
    // UI only
    project(':ontrack-ui').afterEvaluate {
        from(project(':ontrack-ui').tasks.jar) {
            rename { x -> 'ontrack-ui.jar' }
        }
    }
    // Acceptance only
    project(':ontrack-acceptance').afterEvaluate {
        from(project(':ontrack-acceptance').tasks.jar) {
            rename { x -> 'ontrack-acceptance.jar' }
        }
    }
}

build.dependsOn deliveryPackage