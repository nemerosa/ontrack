/**
 * Management of the module exportation for DSL-related modules of Ontrack.
 *
 * Hints:
 *
 * In order to publish to the local repository:
 *
 *      ./gradlew publishToMavenLocal
 *
 */

/**
 * List of modules to export
 */

def exporting = [
        ':ontrack-dsl',
        ':ontrack-client',
        ':ontrack-json',
        ':ontrack-common',
]

/**
 * Configuration Maven for all the modules to export
 */

exporting.each { path ->
    project(path).with {

        // Maven tasks available
        apply plugin: 'maven-publish'

        // Signing available
        apply plugin: 'signing'

        // Javadoc generation
        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        // Source generation
        task sourceJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        // Archives for Javadoc and Sources
        artifacts {
            archives javadocJar, sourceJar
        }

        // Scope of the signatures
        signing {
            sign configurations.archives
        }

        // Publication settings
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact javadocJar
                    artifact sourceJar
                    pom.withXml {
                        asNode().appendNode('url', 'http://nemerosa.github.io/ontrack')
                    }
                }
            }
        }

    }
}