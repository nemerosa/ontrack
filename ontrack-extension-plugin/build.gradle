description "Gradle plugin to create an Ontrack extension."

apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'com.moowork.gradle:gradle-node-plugin:0.11'
    compile 'org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    from groovydoc
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// POM generation
task writeNewPom << {
    pom {
        project {
            version versioning.info.display
            name project.name
            description project.description
            url 'http://nemerosa.github.io/ontrack'
            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/nemerosa/ontrack'
                connection 'scm:git://github.com/nemerosa/ontrack'
                developerConnection 'scm:git:git://github.com/nemerosa/ontrack'
            }
            developers {
                developer {
                    id 'dcoraboeuf'
                    name 'Damien Coraboeuf'
                    email 'damien.coraboeuf@gmail.com'
                }
            }
        }
    }.writeTo("${buildDir}/poms/${project.name}-${version}.pom")
}

// Archives for Javadoc and Sources
artifacts {
    archives javadocJar
    archives sourceJar
    archives file: file("${buildDir}/poms/${project.name}-${version}.pom"), type: 'pom', builtBy: writeNewPom
}

// Links with assembly
assemble.dependsOn 'writeNewPom'
