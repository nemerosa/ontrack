apply plugin: 'maven'

/**
 * Meta information
 */


apply from: "$rootDir/gradle/versioning.gradle"

allprojects {
    group = 'net.nemerosa.ontrack'
    version = rootProject.versionFull
}

/**
 * Dependency management
 */

apply from: "$rootDir/gradle/dependencies.gradle"

/**
 * Java projects
 */

sourceCompatibility = 1.8
targetCompatibility = 1.8

subprojects { project ->

    if (project.path != ':ontrack-web') {
        project.apply plugin: 'java'

        project.dependencies {
            // TODO Lombok not to be released
            compile libraries.lombok
            // Testing
            testCompile libraries.junit
            testCompile libraries.mockito
        }

        // Release task
        task release(dependsOn: 'build') {}
    }

}

/**
 * Task to display the version information
 */
task displayVersion << {
    println "ontrack.version.full = ${project.version}"
    println "ontrack.version.base = ${project.versionBase}"
    println "ontrack.version.build = ${project.versionBuild}"
    println "ontrack.version.commit = ${project.versionCommit}"
    println "ontrack.version.source = ${project.versionSource}"
}

/**
 * Release task.
 *
 * Use the full version to create a tag.
 */
task release << {
    "git tag ${version}".execute()
}

/**
 * Wrapper definition
 */

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
