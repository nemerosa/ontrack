/**
 * Production tasks
 *
 * Standalone tasks.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.nemerosa:http-client-json:1.1.0'
        classpath 'net.nemerosa.ontrack:ontrack-dsl:2.12.4'
    }
}

import net.nemerosa.httpclient.*
import net.nemerosa.httpclient.json.*
import net.nemerosa.ontrack.gradle.AbstractCDTask
import net.nemerosa.ontrack.gradle.DOSetup
import net.nemerosa.ontrack.gradle.DockerOntrackStart
import net.nemerosa.ontrack.gradle.DockerStop
import net.nemerosa.ontrack.gradle.RemoteAcceptanceTest

class ProductionRestore extends AbstractCDTask {

    /**
     * Location of the TGZ backup file
     */
    File backup

    @TaskAction
    void run() {
        // Copies the TGZ on the remote machine
        execute 'docker-machine', 'scp', backup.absolutePath, "${project.properties.productionMachine}:/var/ontrack/${backup.name}"
        // Extracts the TGZ
        execute 'docker-machine', 'ssh', project.properties.productionMachine, "tar xzvf /var/ontrack/${backup.name} -C /var/ontrack/data"
    }

}

class ProductionVersion extends DefaultTask {

    private String version

    @TaskAction
    void run() {
        def client = new JsonClientImpl(
                ClientBuilder
                        .create(project.properties.productionUrl, false)
                        .withLogger({ println "[${name}][HTTP] ${it}" })
                        .build(),
                true
        )
        try {
            version = client.get('info').version.full.asText()
        } catch (ClientGeneralException ignored) {
            println "[${name}] Cannot connect to the production URL at ${project.properties.productionUrl}"
            version = null
        }
        println "[${name}] Version = ${version}"
    }

    String getVersion() {
        return version
    }
}

task productionSetup(type: DOSetup) {
    apiToken = project.properties.digitalOceanAccessToken
    dropletName = project.properties.productionMachine
    region = project.properties.productionRegion
    size = '1gb'
    backups = true
}

task productionEnv {
    mustRunAfter productionSetup
    doFirst {
        project.exec {
            executable 'docker-machine'
            args = ['ssh', project.properties.productionMachine, 'mkdir -p /var/ontrack']
        }
        project.exec {
            executable 'docker-machine'
            args = ['ssh', project.properties.productionMachine, 'mkdir -p /var/ontrack/data']
        }
        project.exec {
            executable 'docker-machine'
            args = ['ssh', project.properties.productionMachine, 'rm -rf /var/ontrack/conf']
        }
        project.exec {
            executable 'docker-machine'
            args = ['scp', '-r', project.properties.productionConf, "${project.properties.productionMachine}:/var/ontrack/conf"]
        }
    }
}

task productionVersion(type: ProductionVersion)

task productionBackup {
    dependsOn 'productionVersion'
    doFirst {
        // Classifier
        def version = productionVersion.version
        def classifier = version ?: new Date().format('yyyMMddHHmmss')
        // Paths
        def remotePath = "/var/ontrack/backup-${classifier}.tgz"
        def localPath = new File(buildDir, "backup-${classifier}.tgz").absolutePath
        // Creates a TAR
        println "[${name}] Creating backup on ${project.properties.productionMachine} at ${remotePath}"
        project.exec {
            executable 'docker-machine'
            args = [
                    'ssh', project.properties.productionMachine,
                    "tar czvf ${remotePath} --exclude=git --exclude=log -C /var/ontrack/data ."
            ]
        }
        // Creates the local directory
        buildDir.mkdirs()
        // Gets the TAR locally
        println "[${name}] Getting local backup at ${localPath}"
        project.exec {
            executable 'docker-machine'
            args = [
                    'scp',
                    "${project.properties.productionMachine}:${remotePath}",
                    localPath
            ]
        }
    }
}

task productionStop(type: DockerStop) {
    dependsOn 'productionBackup'
    machine = project.properties.productionMachine
    containerName = 'ontrack'
    remove = true
    removeVolumes = false
    ignoreError = true
    logFile = null
}

if (hasProperty('backup')) {
    task productionRestore(type: ProductionRestore) {
        // dependsOn productionStop
        backup = new File(project.properties.backup as String)
    }
}

task productionStart(type: DockerOntrackStart) {
    dependsOn 'productionStop'
    machine = project.properties.productionMachine
    exposePort = true
    restart = true
    containerName = 'ontrack'
    image = "nemerosa/ontrack:${project.properties.ontrackVersion}"
    conf = file('/var/ontrack/conf') // Dir on the host, provisioned initially by productionEnv
    data = file('/var/ontrack/data') // Dir on the host, filled in by the application itself
    urandom = false // Not using the urandom source for production
    volumes = [
            '/dev/random': '/dev/random', // Using the random source from the host
    ]
    profile = 'prod'
    mustRunAfter productionEnv
}

task productionTest(type: RemoteAcceptanceTest) {
    disableSsl = false // Production test must use a valid SSL certificate
    acceptanceContext = 'production'
    acceptanceUrl = project.properties.productionUrl
    acceptanceTimeout = 600 // 10 minutes for the startup...
    acceptanceImplicitWait = 30
}

task productionInstall {
    dependsOn productionSetup
    dependsOn productionEnv
    dependsOn productionStart
}

task productionUpgrade {
    dependsOn productionStart
}
